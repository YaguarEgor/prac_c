analyze -> LEX_PROGRAM  B  LEX_FIN

P     -> LEX_PROGRAM  B

B     -> LEX_LFIG  BD  SL  LEX_RFIG    // {описания операторы}

BD    -> D  BD | ε

D     -> LEX_ID  DL  LEX_COLON  TYPE   // описание

DL    -> LEX_COMMA  LEX_ID  DL | ε

TYPE  -> LEX_INT | LEX_BOOL | LEX_STRING

SL    -> S  LEX_SEMICOLON  SL | ε

S     -> LEX_ID     LEX_ASSIGN  E
        | LEX_IF      LEX_LPAREN  E  LEX_RPAREN  S
        | LEX_WHILE   LEX_LPAREN  E  LEX_RPAREN  S
        | LEX_READ    LEX_LPAREN  LEX_ID  LEX_RPAREN
        | LEX_WRITE   LEX_LPAREN  E  WL  LEX_RPAREN
        | LEX_BREAK
        | B

WL    -> LEX_COMMA  E  WL | ε

E     -> E1  ER | E   LEX_OR  E   // дизъюнкция более низкого приоритета

ER    -> LEX_EQ   E1
        | LEX_NEQ  E1
        | LEX_LSS  E1
        | LEX_GTR  E1
        | LEX_LEQ  E1
        | LEX_GEQ  E1
        | ε

E1     -> T  E1'

E1'    -> LEX_PLUS   T  E1'
        | LEX_MINUS  T  E1'
        | ε

T      -> F  T'

T'     -> LEX_TIMES  F  T'
        | LEX_SLASH  F  T'
        | LEX_MOD    F  T'
        | LEX_AND    F  T'
        | ε

F     -> LEX_ID
        | LEX_NUM
        | LEX_STR
        | LEX_TRUE
        | LEX_FALSE
        | LEX_NOT     F
        | LEX_LPAREN  E  LEX_RPAREN